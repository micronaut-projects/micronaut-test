plugins {
    id 'org.graalvm.buildtools.native' version '0.9.10'
    id "io.micronaut.build.internal.base-module"
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

tasks.withType(Test).configureEach {
    jvmArgs '-Duser.country=US'
    jvmArgs '-Duser.language=en'

    reports.html.required = !System.getenv("GITHUB_ACTION")
    reports.junitXml.required = true

    useJUnitPlatform()
}

configurations {
    nativeImageTestClasspath {
        exclude(group: 'org.codehaus.groovy')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

tasks.withType(Jar).configureEach {
    manifest {
        attributes('Automatic-Module-Name': "${project.group}.${project.name}".replaceAll('[^\\w\\.\\$_]', "_"))
        attributes('Implementation-Version': project.findProperty("projectVersion"))
        attributes('Implementation-Title': project.findProperty("title"))
    }
}

dependencies {
    api project(":test-core")
    api "io.micronaut:micronaut-inject"
    api "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    implementation "io.micronaut:micronaut-runtime"
    compileOnly "org.graalvm.nativeimage:svm:22.0.0.2"

    testCompileOnly "io.micronaut:micronaut-inject"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor "io.micronaut:micronaut-graal"

    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "io.micronaut:micronaut-http-server-netty"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation 'org.mockito:mockito-junit-jupiter:$mockitoVersion'
    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
    testImplementation "io.micronaut.data:micronaut-data-tx"
    testImplementation "io.micronaut.data:micronaut-data-hibernate-jpa"

    testRuntimeOnly "io.micronaut.sql:micronaut-jdbc-tomcat"
    testRuntimeOnly "com.h2database:h2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

graalvmNative {
    binaries {
        test {
            buildArgs.add('--initialize-at-run-time=io.micronaut.test.junit5.server.ExternalServerTest')
            resources.autodetect()
        }
    }
}

test {
    systemProperty("mockito.test.enabled", "true")
}
