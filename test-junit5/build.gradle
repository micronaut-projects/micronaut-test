plugins {
    id 'org.graalvm.buildtools.native' version '0.9.10'
    id "io.micronaut.build.internal.base-module"
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

tasks.withType(Test).configureEach {
    jvmArgs '-Duser.country=US'
    jvmArgs '-Duser.language=en'

    reports.html.required = !System.getenv("GITHUB_ACTION")
    reports.junitXml.required = true

    useJUnitPlatform()
}

configurations {
    nativeImageTestClasspath {
        exclude(group: 'org.codehaus.groovy')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

tasks.withType(Jar).configureEach {
    manifest {
        attributes('Automatic-Module-Name': "${project.group}.${project.name}".replaceAll('[^\\w\\.\\$_]', "_"))
        attributes('Implementation-Version': project.findProperty("projectVersion"))
        attributes('Implementation-Title': project.findProperty("title"))
    }
}

dependencies {
    api project(":test-core")
    api(libs.micronaut.inject)
    api(libs.junit.jupiter.api)
    implementation(libs.micronaut.runtime)
    compileOnly(libs.graal)

    testCompileOnly(libs.micronaut.inject)

    testAnnotationProcessor(libs.micronaut.inject.java)
    testAnnotationProcessor(libs.micronaut.graal)

    testImplementation(libs.junit.jupiter.params)
    testImplementation(libs.micronaut.http.server.netty)
    testImplementation(libs.micronaut.http.client)
    testImplementation(libs.mockito.junit.jupiter)
    testImplementation(libs.hamcrest)
    testImplementation(libs.micronaut.data.tx)
    testImplementation(libs.micronaut.data.hibernate.jpa)

    testRuntimeOnly(libs.micronaut.jdbc.tomcat)
    testRuntimeOnly(libs.h2)
    testRuntimeOnly(libs.junit.jupiter.engine)
}

graalvmNative {
    binaries {
        test {
            buildArgs.add('--initialize-at-run-time=io.micronaut.test.junit5.server.ExternalServerTest')
            resources.autodetect()
        }
    }
}

test {
    systemProperty("mockito.test.enabled", "true")
}
