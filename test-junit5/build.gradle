plugins {
    id 'org.graalvm.buildtools.native' version '0.9.10'
    id "io.micronaut.build.internal.base-module"
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

tasks.withType(Test).configureEach {
    jvmArgs '-Duser.country=US'
    jvmArgs '-Duser.language=en'

    reports.html.required = !System.getenv("GITHUB_ACTION")
    reports.junitXml.required = true

    useJUnitPlatform()
}

configurations {
    nativeImageTestClasspath {
        exclude(group: 'org.codehaus.groovy')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

tasks.withType(Jar).configureEach {
    manifest {
        attributes('Automatic-Module-Name': "${project.group}.${project.name}".replaceAll('[^\\w\\.\\$_]', "_"))
        attributes('Implementation-Version': project.findProperty("projectVersion"))
        attributes('Implementation-Title': project.findProperty("title"))
    }
}

dependencies {
    api project(":test-core")
    api "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-runtime"
    compileOnly "org.graalvm.nativeimage:svm:21.3.0"
    api("org.junit.jupiter:junit-jupiter-api:$junitVersion")

    testCompileOnly "io.micronaut:micronaut-inject"
    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    )

    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor "io.micronaut:micronaut-graal"
    testImplementation "io.micronaut:micronaut-http-server-netty"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation 'org.mockito:mockito-junit-jupiter:4.1.0'
    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
    testImplementation "io.micronaut.data:micronaut-data-tx:2.4.7"
    testImplementation "io.micronaut.data:micronaut-data-hibernate-jpa:2.4.7"

    testRuntimeOnly "io.micronaut.sql:micronaut-jdbc-tomcat:$micronautSqlVersion"
    testRuntimeOnly "com.h2database:h2:1.4.200"

}

graalvmNative {
    binaries {
        test {
            buildArgs.add('--initialize-at-run-time=io.micronaut.test.junit5.server.ExternalServerTest')
            resources.autodetect()
        }
    }
}

test {
    systemProperty("mockito.test.enabled", "true")
}
