buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build.internal:micronaut-gradle-plugins:4.0.0"
    }
}

repositories {
    maven { url "https://repo.grails.org/grails/core" }
}
    
subprojects { Project subproject ->
    group "io.micronaut.test"

    if (subproject.name != 'test-bom') {
        apply plugin: "io.micronaut.build.internal.common"

        micronautBuild {
            resolutionStrategy {
                eachDependency { DependencyResolveDetails details ->
                    String group = details.requested.group
                    if (group.startsWith('org.jetbrains.kotlin') && group != 'org.jetbrains.kotlinx') {
                        details.useVersion kotlinVersion
                    }
                }
            }
        }

    } else {
        apply plugin: "java-platform"
        version projectVersion
    }

    apply plugin: "io.micronaut.build.internal.dependency-updates"
    apply plugin: "io.micronaut.build.internal.publishing"
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"
