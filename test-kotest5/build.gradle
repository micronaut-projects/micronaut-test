plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.kapt" version "$kotlinVersion"
    id "io.micronaut.build.internal.module"
}

repositories {
    mavenCentral()
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    api project(":test-core")
    api(mn.micronaut.context)
    api(libs.managed.kotest.runner.junit5.jvm)

    implementation(mn.micronaut.inject)
    implementation project(":test-junit5")
    implementation(libs.kotlin.stdlib.jdk8)
    implementation(libs.kotlin.reflect)

    compileOnly(libs.jakarta.inject.api)

    testCompileOnly(libs.jakarta.inject.api)

    kaptTest(platform(mn.micronaut.bom))
    kaptTest(mn.micronaut.inject.java)

    testImplementation(libs.managed.mockk)

    testImplementation(mn.micronaut.http.server.netty)
    testImplementation(mn.micronaut.http.client)

    testImplementation(mn.micronaut.data.tx)
    testImplementation(mn.micronaut.data.hibernate.jpa)

    testRuntimeOnly(mn.micronaut.jdbc.tomcat)
    testRuntimeOnly(libs.h2)
    testImplementation(mn.snakeyaml)
    testImplementation(libs.managed.kotest.assertions.core.jvm)
}

test {
    useJUnitPlatform {
        includeEngines("kotest")
    }
    // PTS fails for Kotest because it's not supported, yet.
    // see https://docs.gradle.com/enterprise/predictive-test-selection/#_frameworks_and_languages
    predictiveSelection.enabled = false
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        javaParameters = true
    }
}

// TODO temporarily disable binary compatibility checks
micronautBuild {
    binaryCompatibility {
        enabled = false
    }
}
